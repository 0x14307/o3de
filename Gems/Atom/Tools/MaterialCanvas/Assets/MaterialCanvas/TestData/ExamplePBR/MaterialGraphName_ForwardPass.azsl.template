/*
 * Copyright (c) Contributors to the Open 3D Engine Project.
 * For complete copyright and license terms please see the LICENSE at the root of this distribution.
 *
 * SPDX-License-Identifier: Apache-2.0 OR MIT
 *
 */

#include <viewsrg.srgi>
#include <Atom/Features/PBR/DefaultObjectSrg.azsli>
#include <Atom/Features/PBR/ForwardPassSrg.azsli>
#include <Atom/Features/PBR/ForwardPassOutput.azsli>
#include <Atom/Features/PBR/AlphaUtils.azsli>
#include <Atom/Features/SrgSemantics.azsli>
#include <Atom/Features/ColorManagement/TransformColor.azsli>
#include <Atom/Features/PBR/Lighting/StandardLighting.azsli>
#include <Atom/Features/PBR/Decals.azsli>

struct VSInput
{
    // These will eventually also be filled in by the material graph
    float3 m_position : POSITION;
    float3 m_normal : NORMAL;
    float4 m_tangent : TANGENT; 
    float3 m_bitangent : BITANGENT; 

    // GENERATED_VSINPUT_BEGIN
    // GENERATED_VSINPUT_END
};

struct VSOutput
{
    precise linear centroid float4 m_position : SV_Position;
    float3 m_normal: NORMAL;
    float3 m_tangent : TANGENT; 
    float3 m_bitangent : BITANGENT; 
    float3 m_worldPosition : UV0;
};

#include <Atom/Features/Vertex/VertexHelper.azsli>

ShaderResourceGroup MaterialSrg : SRG_PerMaterial
{
    // Auto-generate material SRG fields from MaterialGraphName
    // GENERATED_MATERIAL_SRG_BEGIN
    // GENERATED_MATERIAL_SRG_END
}
 
// GENERATED_CLASSES_BEGIN
// GENERATED_CLASSES_END
 
// GENERATED_FUNCTIONS_BEGIN
// GENERATED_FUNCTIONS_END

// One possibility for generating code for each input on the lighting model output node is to use one function per input on that node. Each
// of these functions resulting value will be initialized with a default value and calling the functions as is should allow the template
// shader code to compile even without generated data. This will allow iteration and testing while building material graph templates.
// Alternatively, all of the nodes can be evaluated and code generated inline in the main fragment shader function. That may be more
// desirable if a lot of the generated code is shared between them.
float4 GetBaseColorInput(VSOutput IN)
{
    float4 result = float4(1.0, 1.0, 1.0, 1.0);
    // GENERATED_VSOUTPUT_FUNCTION_BEGIN BaseColor
    // GENERATED_VSOUTPUT_FUNCTION_END
    return result;
}
 
float GetMetallicInput(VSOutput IN)
{
    float result = 1.0;
    // GENERATED_VSOUTPUT_FUNCTION_BEGIN Metallic
    // GENERATED_VSOUTPUT_FUNCTION_END
    return result;
}
 
float GetRoughnessInput(VSOutput IN)
{
    float result = 0.5;
    // GENERATED_VSOUTPUT_FUNCTION_BEGIN Roughness
    // GENERATED_VSOUTPUT_FUNCTION_END
    return result;
}
 
float GetSpecularF0Input(VSOutput IN)
{
    float result = 0.5;
    // GENERATED_VSOUTPUT_FUNCTION_BEGIN SpecularF0
    // GENERATED_VSOUTPUT_FUNCTION_END
    return result;
}
 
VSOutput MaterialGraphName_MainPassVS(VSInput IN)
{
    VSOutput OUT;
 
    float3 worldPosition = mul(ObjectSrg::GetWorldMatrix(), float4(IN.m_position, 1.0)).xyz;
 
    VertexHelper(IN, OUT, worldPosition);

    return OUT;
}

ForwardPassOutput MaterialGraphName_MainPassPS(VSOutput IN)
{
    // ------- Surface -------

    Surface surface;
    
    // Position, Normal, Roughness
    surface.position = IN.m_worldPosition.xyz;
    surface.normal = normalize(IN.m_normal);
    surface.vertexNormal = normalize(IN.m_normal);
    surface.roughnessLinear = GetRoughnessInput(IN);
    surface.CalculateRoughnessA();

    // Albedo, SpecularF0
    float4 baseColor = GetBaseColorInput(IN);
    float metallic = GetMetallicInput(IN);
    float specularF0Factor = GetSpecularF0Input(IN);
    surface.SetAlbedoAndSpecularF0(baseColor, specularF0Factor, metallic);

    // Clear Coat
    surface.clearCoat.InitializeToZero();

    // ------- LightingData -------

    LightingData lightingData;

    // Light iterator
    lightingData.tileIterator.Init(IN.m_position, PassSrg::m_lightListRemapped, PassSrg::m_tileLightData);
    lightingData.Init(surface.position, surface.normal, surface.roughnessLinear);


    // Diffuse and Specular response
    lightingData.specularResponse = FresnelSchlickWithRoughness(lightingData.NdotV, surface.specularF0, surface.roughnessLinear);
    lightingData.diffuseResponse = 1.0f - lightingData.specularResponse;

    const float alpha = 1.0f;

    // ------- Lighting Calculation -------

    // Apply Decals
    ApplyDecals(lightingData.tileIterator, surface);

    // Apply Direct Lighting
    ApplyDirectLighting(surface, lightingData, IN.m_position);

    // Apply Image Based Lighting (IBL)
    ApplyIBL(surface, lightingData);

    // Finalize Lighting
    lightingData.FinalizeLighting();

    PbrLightingOutput lightingOutput = GetPbrLightingOutput(surface, lightingData, alpha);

    // ------- Output -------

    ForwardPassOutput OUT;

    OUT.m_diffuseColor = lightingOutput.m_diffuseColor;
    OUT.m_diffuseColor.w = -1; // Subsurface scattering is disabled
    OUT.m_specularColor = lightingOutput.m_specularColor;
    OUT.m_specularF0 = lightingOutput.m_specularF0;
    OUT.m_albedo = lightingOutput.m_albedo;
    OUT.m_normal = lightingOutput.m_normal;

    return OUT;
}